dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.31 2004/05/12 02:40:34 mschimek Exp $

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(zvbi, 0.2.8)
AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE(m4)

dnl curr (added interf) : rev (of curr) : age (curr-age compat)
AC_SUBST(LIBZVBI_SO_VERSION, [3:2:3])

AC_PROG_CC
AC_PROG_CXX
AM_PROG_LIBTOOL

test -e site_def.h || cat <<EOF >site_def.h
/* Site specific definitions */

#ifndef SITE_DEF_H
#define SITE_DEF_H
/* #define CACHE_DEBUG 1 */
/* #define CACHE_STATUS 1 */
/* #define DECODER_PATTERN_DUMP 1 */
/* #define DECODER_SERVICE_DIAG 1 */
#endif /* SITE_DEF_H */
EOF

dnl
dnl Check how to link pthreads functions.
dnl
AC_CHECK_LIB(pthread, pthread_create,,[
  AC_TRY_LINK(, pthread_create();,,[
    LDFLAGS="$LDFLAGS -pthread"
    AC_TRY_LINK(, pthread_create();,,[
      AC_MSG_ERROR([Unable to link pthread functions])
    ])
  ])
])

dnl
dnl Check for Gnome unicode library or libc 2.1.
dnl (Teletext URE search wchar_t ctype.h functions)
dnl
AC_MSG_CHECKING(whether we are using the GNU C Library 2.1 or newer)
AC_EGREP_CPP([GNU 98765876],[
#include <features.h>
#ifdef __GNU_LIBRARY__
 #if (__GLIBC__ == 2 && __GLIBC_MINOR__ >= 1) || (__GLIBC__ > 2)
  GNU 98765876
 #endif
#endif
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_GLIBC21, 1, [Define if you have GNU C Lib 2.1+])
],[
  AC_MSG_RESULT([no])
  AC_MSG_CHECKING(for unicode library)
  UNICODE_VERSION=`unicode-config --version`
  if test $? -eq 0; then
    AC_DEFINE(HAVE_LIBUNICODE, 1, [Define if you have libunicode])
    AC_MSG_RESULT($UNICODE_VERSION)
    UNICODE_CFLAGS=`unicode-config --cflags`
    UNICODE_LIBS=`unicode-config --libs`
    AC_SUBST(UNICODE_CFLAGS)
    AC_SUBST(UNICODE_LIBS)
  else
    AC_MSG_RESULT([not present - Teletext search disabled])
  fi
])

dnl
dnl Check for iconv() in libc or libiconv.
dnl (Unicode conversions)
dnl
dnl Using m4/iconv.m4 from the gettext package.
dnl
AM_ICONV_LINK
if test "$am_cv_func_iconv" != yes; then
  AC_MSG_ERROR([iconv() not found])
fi
LIBS="$LIBS $LIBICONV"

dnl
dnl Check for png library.
dnl (PNG page export)
dnl
PNG_LIB=""
HAVE_PNG="yes"
AC_CHECK_LIB(png, png_destroy_write_struct,
  PNG_LIB="-lpng -lz -lm", HAVE_PNG="no", -lz -lm)
if test "$HAVE_PNG" = yes; then
  AC_DEFINE(HAVE_LIBPNG, 1, [Define if you have libpng])
fi
AC_SUBST(PNG_LIB)

dnl
dnl X libraries.
dnl (Test programs)
dnl
AC_PATH_XTRA
test "$no_x" = yes || X_LIBS="$X_LIBS -lX11"

dnl
dnl GNU getopt_long
dnl (Test programs)
dnl
AC_CHECK_FUNCS(getopt_long)

dnl
dnl getaddrinfo
dnl (Proxy service via TCP/IP)
dnl
dnl AC_CHECK_FUNCS()

dnl
dnl Enable OS dependent device interfaces.
dnl (Linux videodev.h, videodev2.h, dvb, bktr dependencies)
dnl
enable_v4l_auto=no
enable_dvb_auto=no
enable_bktr_auto=no

case "`uname -s`" in
  Linux)
    enable_v4l_auto=yes
    enable_dvb_auto=yes
    ;;
  OpenBSD | FreeBSD | NetBSD | BSDi)
    enable_bktr_auto=yes
    ;;
  *)
    ;;
esac

AC_MSG_CHECKING([whether to build the Video4Linux interface])
AC_ARG_ENABLE(v4l,
  AC_HELP_STRING([--enable-v4l],
  [Include the V4L and V4L2 interface (auto)]),,
  enable_v4l=$enable_v4l_auto)
AC_MSG_RESULT($enable_v4l)
if test "$enable_v4l" = yes; then
  AC_DEFINE(ENABLE_V4L, 1, [Define to build V4L interface])
  AC_DEFINE(ENABLE_V4L2, 1, [Define to build V4L2 / V4L2 2.5 interface])
fi

AC_MSG_CHECKING([whether to build the Linux DVB interface])
AC_ARG_ENABLE(dvb,
  AC_HELP_STRING([--enable-dvb],
  [Include the DVB interface (auto)]),,
  enable_dvb=$enable_dvb_auto)
AC_MSG_RESULT($enable_dvb)
if test "$enable_dvb" = yes; then
  AC_DEFINE(ENABLE_DVB, 1, [Define to build DVB interface])
fi

AC_MSG_CHECKING([whether to build the *BSD bktr driver interface])
AC_ARG_ENABLE(bktr,
  AC_HELP_STRING([--enable-bktr],
  [Include the *BSD bktr driver interface (auto)]),,
  enable_bktr=$enable_bktr_auto)
AC_MSG_RESULT($enable_bktr)
if test "$enable_bktr" = yes; then
  AC_DEFINE(ENABLE_BKTR, 1, [Define to build bktr driver interface])
fi

dnl
dnl Enable vbi proxy
dnl
dnl AC_MSG_CHECKING([whether to build the vbi proxy daemon and interface])
dnl AC_ARG_ENABLE(proxy,
dnl   AC_HELP_STRING([--enable-proxy],
dnl   [Build the vbi proxy daemon and interface (yes)]),,
dnl   enable_proxy=yes)
dnl AC_MSG_RESULT($enable_proxy)
dnl if test "$enable_proxy" = yes; then
dnl   AC_DEFINE(ENABLE_PROXY, 1,
dnl   [Define to build proxy daemon and and interface])
dnl fi
dnl AM_CONDITIONAL(ENABLE_PROXY, [test "$enable_proxy" = yes])

dnl
dnl Native language support.
dnl
AM_GNU_GETTEXT([external], [need-ngettext])
LIBS="$LTLIBINTL $LIBS"
if test "${prefix}" = NONE; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR,
    "${ac_default_prefix}/share/locale", [ld])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR,
    "${prefix}/share/locale", [ld])
fi

dnl 
dnl Build docs from the sources if Doxygen is installed.
dnl 
AC_CHECK_PROG(HAVE_DOXYGEN, doxygen, yes, no)
AM_CONDITIONAL(HAVE_DOXYGEN, [test "$HAVE_DOXYGEN" = yes])

dnl See proxy branch
dnl daemon/Makefile
dnl daemon/zvbid.init

AC_OUTPUT([
  Makefile
  po/Makefile.in
  src/Makefile
  src/dvb/Makefile
  test/Makefile
  doc/Makefile
  doc/Doxyfile
  contrib/Makefile
  m4/Makefile
  zvbi.spec
])
